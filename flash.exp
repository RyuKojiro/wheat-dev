#!/usr/bin/expect

log_user 0

source vars.exp

set spawned [spawn -open [open $port w+]]
exec stty -f $port $baud raw -clocal -echo
set file [lindex $argv 0]

proc ledDisplayPrint {string} {
	expect ">" {
		send "m BA202070\r";
	}

	expect "BA202070" {
		for {set i 0} {$i < 8} {incr i} {
			sleep .01
			send [format %4.4X [scan [string index $string $i] %c]]
			send "\rFF\r"
		}
		send ".\r"
	}
}

proc sendFile {filename} {
	set offset 0x08000000
	set addr 0
	set len [file size $filename]
	set start [clock milliseconds]
	expect ">" {
		send "lf\r";
	}

	expect "please send ! ('.' & CR stop load)" {
		set fp [open $filename r]
		chan configure $fp -translation binary
		while {$addr < $len} {
			send [read $fp 1]
			incr addr

			set now [clock milliseconds]
			set percent [expr 100 * $addr / $len]
			set rate [format %.2f [expr $addr / (($now - $start) / 1000.0)]]
			send_user "$addr/$len bytes - $rate bytes/sec - $percent%\r"
		}
		close $fp
		send ".\r"
		send_user "\n"
	}
}

send_user "Waiting for remote side…\n"
send "\r"

ledDisplayPrint "Flashing"
send_user "Sending $file…\n"
sendFile $file
ledDisplayPrint "  Done! "

# Ensure the entire send got through
sleep 1

#interact
